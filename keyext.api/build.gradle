plugins {
    id 'application'

    // Used to create a single executable jar file with all dependencies
    // see task "shadowJar" below
    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version "8.1.1"

    id 'com.google.protobuf' version '0.9.4'
}

description "Verification server interface via JSON-RPC"


repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/"
    }
}

dependencies {
    api(project(":key.core"))
    api(project(":key.ui"))

    implementation("info.picocli:picocli:4.7.5")
    implementation("com.google.guava:guava:32.1.3-jre")

    //for GraalVM annotationProcessor 'info.picocli:picocli-codegen:4.7.5'
}


// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.68.1' // CURRENT_GRPC_VERSION
def protobufVersion = '3.25.5'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "io.grpc:grpc-inprocess:${grpcVersion}"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:4.4.0"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

compileJava {
    // for GraalVM options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
    options.compilerArgs += ["-parameters"] // for having parameter name in reflection
}

