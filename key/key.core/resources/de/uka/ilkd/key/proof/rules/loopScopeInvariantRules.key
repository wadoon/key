\schemaVariables {
    \modalOperator { diamond, box, diamond_transaction, box_transaction } #allmodal;
    \formula post;
    \formula b;
    \program Statement #body;
    \program Expression #nse;
    \program Variable #x, #heapBeforeLoop;
    \formula #inv;
    \term int #variant;
    \update #anon_update;
    \program LoopInit #loopInit;
    \program Guard #guard;
    \program ForUpdates #forupdates; 
    \skolemTerm Heap anonHeap;
}

\rules(programRules:Java) {
    loopScopeInvariantTacletWhile { 
        \find (
            (\modality{#allmodal} {.. while (#nse) #body ...} \endmodality(post))
        )
          
        \varcond(\new(#x, boolean), \new(#heapBeforeLoop, Heap), \getInvariant(#inv, #allmodal))
    
        "Invariant Initially Valid":
        \replacewith(#inv & wellFormed(heap));
    
        "Invariant Preserved and Used":
        \replacewith
        (
            {#anonUpdateTransformer(\modality{#allmodal}{}\endmodality(true), #heapBeforeLoop, anonHeap)}
            (#inv & #wellFormedAnonHeap(\modality{#allmodal}{}\endmodality(true), anonHeap) ->
                (\modality{#allmodal} { ..
                    boolean #x; #x = true;
                    loop-scope(#x) {
                        if (#nse) 
                        { #body #x = false; }
                    } ... 
				}\endmodality(((	
					(#x = TRUE) -> post) & 
					((#x = FALSE) -> 
						#inv
                        & #getFrameCondition(\modality{#allmodal}{}\endmodality(true), #heapBeforeLoop)
                        & #getVariant(post))))
                )
            )
        )
    
        \heuristics(loop_inv_taclets)
    };
    
    loopScopeInvariantTacletFor { 
        \find (
            (\modality{#allmodal} {.. for(; #guard; #forupdates) #body ... }\endmodality(post))
        )
        
        \varcond(\new(#x, boolean), \new(#heapBeforeLoop, Heap), \getInvariant(#inv, #allmodal), \getGuardExpr(#nse, #guard))
        
        "Invariant Initially Valid":
        \replacewith(#inv & wellFormed(heap));
        
        "Invariant Preserved and Used":
        \replacewith
        (
            {#anonUpdateTransformer(\modality{#allmodal}{}\endmodality(true), #heapBeforeLoop, anonHeap)}
            (#inv & #wellFormedAnonHeap(\modality{#allmodal}{}\endmodality(true), anonHeap) ->
                (\modality{#allmodal} {.. 
					boolean #x; #x = true;
                    loop-scope(#x) {
                        if (#nse) {
                            #body 
                            #x = false; 
                        }
                        if (!#x) {
                            #x = true;
                            #forUpdateUnfoldTransformer(#forupdates);
                            #x = false;
                        }
                    } ...
                }\endmodality(((
					(#x = TRUE) -> post) & 
                    ((#x = FALSE) -> 
						#inv
						& #getFrameCondition(\modality{#allmodal}{}\endmodality(true), #heapBeforeLoop)
						& #getVariant(post))))
                )
            )
        )
    
        \heuristics(loop_inv_taclets) 
    };
}